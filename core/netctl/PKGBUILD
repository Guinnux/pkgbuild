# Maintainer: Jouke Witteveen <j.witteveen@gmail.com>

pkgname=netctl
pkgver=1.11
pkgrel=6
pkgdesc='Profile based systemd network management'
url='http://projects.archlinux.org/netctl.git/'
license=('GPL')
groups=('base')
depends=('coreutils' 'iproute2' 'openresolv' 'systemd' 'ppp' 'libqmi')
# The source tarball includes pre-built (using asciidoc) documentation.
makedepends=('pkg-config')
optdepends=('dialog: for the menu based wifi assistant'
            'dhclient: for DHCP support (or dhcpcd)'
            'dhcpcd: for DHCP support (or dhclient)'
            'wpa_supplicant: for wireless networking support'
            'ifplugd: for automatic wired connections through netctl-ifplugd'
            'wpa_actiond: for automatic wireless connections through netctl-auto'
            'openvswitch: for Open vSwitch connections'
           )
source=(https://sources.archlinux.org/other/packages/netctl/netctl-${pkgver}.tar.xz
        'netctl-ppp@.service'
        'ppp-persistence.patch'
        'ppp.hwinit'
        'led-up.sh'
        'led-down.sh'
        'if_bind.patch'
        'wwan')

arch=('any')

md5sums=('1465a0595af16fd88a5f1ecdcebc0f0c'
         'aab340dd11349e34ccebbad50d9ae9da'
         '8618a3bd8d6ed9a3d0654ae060ae156c'
         '983dde79eb5c56c6a4ee848d7377adec'
         '95137643cb83bc8a6036ad8284cef1f4'
         'b4e9731ec9c2adf6de1f3c05ac00b324'
         '8569c0099549332c71de85a235c758a9'
         '42b18246d87dba294ab58c42db24c538')

prepare() {
	cd "$srcdir/netctl-${pkgver}"
	patch -p0 < "$srcdir/ppp-persistence.patch"
	patch -p0 < "$srcdir/if_bind.patch"
}                


package() {
  cd "$srcdir/netctl-${pkgver}"
  make DESTDIR="$pkgdir" install

  # Shell Completion
  install -D -m644 contrib/bash-completion "$pkgdir/usr/share/bash-completion/completions/netctl"
  install -D -m644 contrib/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_netctl"
  
  cd "$srcdir"
  install -D -m644 "netctl-ppp@.service"  "$pkgdir/usr/lib/systemd/system/netctl-ppp@.service"
  install -D -m755 "ppp.hwinit"  "$pkgdir/etc/netctl/options/ppp.hwinit"
  install -D -m755 "led-up.sh"  "$pkgdir/etc/ppp/ip-up.d/10-led-up.sh"
  install -D -m755 "led-down.sh"  "$pkgdir/etc/ppp/ip-down.d/10-led-down.sh"
  install -m644 wwan "$pkgdir/usr/lib/network/connections/"
}

