# Maintainer: Jouke Witteveen <j.witteveen@gmail.com>

pkgname=netctl
pkgver=1.11
pkgrel=2
pkgdesc='Profile based systemd network management'
url='http://projects.archlinux.org/netctl.git/'
license=('GPL')
groups=('base')
depends=('coreutils' 'iproute2' 'openresolv' 'systemd')
# The source tarball includes pre-built (using asciidoc) documentation.
makedepends=('pkg-config')
optdepends=('dialog: for the menu based wifi assistant'
            'dhclient: for DHCP support (or dhcpcd)'
            'dhcpcd: for DHCP support (or dhclient)'
            'wpa_supplicant: for wireless networking support'
            'ifplugd: for automatic wired connections through netctl-ifplugd'
            'wpa_actiond: for automatic wireless connections through netctl-auto'
            'ppp: for PPP connections'
            'openvswitch: for Open vSwitch connections'
           )
source=(https://sources.archlinux.org/other/packages/netctl/netctl-${pkgver}.tar.xz
        'netctl-ppp@.service'
        'ppp-persistence.patch')

arch=('any')

md5sums=('1465a0595af16fd88a5f1ecdcebc0f0c'
         '2fb91e10de609ce3ceebb69d474dd766'
         'c18b112fb17093f9b8bb7545f0e57963')

prepare() {
	cd "$srcdir/netctl-${pkgver}"
	patch -p0 < "$srcdir/ppp-persistence.patch"
}                


package() {
  cd "$srcdir/netctl-${pkgver}"
  make DESTDIR="$pkgdir" install

  # Shell Completion
  install -D -m644 contrib/bash-completion "$pkgdir/usr/share/bash-completion/completions/netctl"
  install -D -m644 contrib/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_netctl"
  
  cd "$srcdir"
  install -D -m644 "netctl-ppp@.service"  "$pkgdir/usr/lib/systemd/system/netctl-ppp@.service"

}

