# $Id: PKGBUILD 246565 2015-09-19 00:40:05Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Isenmann <daniel @archlinux.org>

pkgbase=dhcp
pkgname=('dhcp' 'dhclient')

# separate patch levels with a period to maintain proper versioning.
pkgver=4.3.3
_pkgver=4.3.3
pkgrel=3
arch=('i686' 'x86_64')
license=('custom:isc-dhcp')
url="https://www.isc.org/software/dhcp"
makedepends=('bash' 'iproute2')
source=(ftp://ftp.isc.org/isc/${pkgbase}/${_pkgver}/${pkgbase}-${_pkgver}.tar.gz
        dhcpd4.service
        dhcpd6.service
        dhclient@.service
        dhcp-4.3.1-client_script-1.patch
        dhcp-4.3.1-missing_ipv6-1.patch
        arch_patch_over_lfs.patch
        bind-makefile.patch
        bind-gen.patch
        dhcpd.conf)

md5sums=('c5577b09c9017cdd319a11ff6364268e'
         '33cceed7056fdceda845eef26934f30f'
         '4db26ed8335b908ef8cdee55172a84ed'
         '06aa2ae46f05fdc784275e6ae54a8f7f'
         'c02bddb6c6c33c5885e3dd072ee2ee40'
         'da655f02f1cf4d95b4708e84e69edbc8'
         'ea45057374098b8ff7e140ecedf7696a'
         '63f0d6f75f2bd5b9f503289355130313'
         '4fa2316845f5b0fe686d33f24def2e7e'
         '8c293f112a39b72bb396245f184faf70')

validpgpkeys=('ADBE9446286C794905F1E0756FA6EBC9911A4C02') # Internet Systems Consortium, Inc. <codesign@isc.org>

prepare() {
  cd "${srcdir}/${pkgbase}-${_pkgver}"

  # Define _GNU_SOURCE to fix IPV6.
  sed '/^CFLAGS="$CFLAGS/ s/INGS"/INGS -D_GNU_SOURCE"/' -i configure

  # Apply patches from LSF
  # Make not having ipv6 non-fatal.
  patch -Np1 -i ../dhcp-4.3.1-missing_ipv6-1.patch
  # Make dhcp client script suck less.
  patch -Np1 -i ../dhcp-4.3.1-client_script-1.patch

  # Apply Arch-specific patches over LSF changes
  patch -Np1 -i ../arch_patch_over_lfs.patch

  cd "${srcdir}/${pkgbase}-${_pkgver}/bind"
  patch -Np0 -i ${srcdir}/bind-makefile.patch
  tar -xf ./bind.tar.gz
  patch -Np0 -i ${srcdir}/bind-gen.patch
}

build() {
  cd "${srcdir}/${pkgbase}-${_pkgver}"
ac_cv_file__dev_random=yes ./configure --prefix=/usr --host=$CHOST --sbindir=/usr/bin --sysconfdir=/etc \
      --with-srv-lease-file=/var/lib/dhcp/dhcpd.leases \
      --with-srv6-lease-file=/var/lib/dhcp/dhcpd6.leases \
      --with-cli-lease-file=/var/lib/dhclient/dhclient.leases \
      --with-cli6-lease-file=/var/lib/dhclient/dhclient6.leases

  make 
}

package_dhcp(){
  pkgdesc="A DHCP server, client, and relay agent"
  depends=('glibc')
  backup=('etc/dhcpd.conf' 'etc/dhcpd6.conf')
  install=dhcp.install
  groups=('base')

  cd "${srcdir}/${pkgbase}-${_pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/var/lib/dhcp"

  install -D -m644 "${srcdir}/dhcpd4.service" "${pkgdir}/usr/lib/systemd/system/dhcpd4.service"
  install -D -m644 "${srcdir}/dhcpd6.service" "${pkgdir}/usr/lib/systemd/system/dhcpd6.service"

  # move back to config. This file is in backup array, so pacman will not overwrite a changed config.
  mv "${pkgdir}/etc/dhcpd.conf.example" "${pkgdir}/etc/dhcpd6.conf"
  install -D -m644 "${srcdir}/dhcpd.conf" "${pkgdir}/etc/dhcpd.conf"

  # Remove dhclient
  make -C client DESTDIR="${pkgdir}" uninstall

  # install license
  install -m644 -D LICENSE "${pkgdir}/usr/share/licenses/dhcp/LICENSE"
}

package_dhclient(){
  pkgdesc="A standalone DHCP client from the dhcp package"
  depends=('glibc' 'bash' 'iproute2')
  provides=('dhcp-client')

  cd "${srcdir}/${pkgbase}-${_pkgver}"
  make -C client DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/dhclient"
  mv "${pkgdir}/etc/dhclient.conf.example" "${pkgdir}/usr/share/dhclient/"

  install -d "${pkgdir}/var/lib/dhclient"

  # install dhclient linux script
  install -m755 client/scripts/linux "${pkgdir}/usr/bin/dhclient-script"

  # install license
  install -m644 -D LICENSE "${pkgdir}/usr/share/licenses/dhclient/LICENSE"

  # install systemd service unit
  install -m644 -D "$srcdir/dhclient@.service" "${pkgdir}/usr/lib/systemd/system/dhclient@.service"
}
