# $Id: PKGBUILD 272470 2016-07-26 15:35:52Z lcarlier $
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgbase=vulkan-docs
pkgname=(vulkan-headers vulkan-man-pages vulkan-html-docs)
pkgver=1.0.21
pkgrel=2
epoch=1
_pkgname=Vulkan-Docs
_pkgver=1.0-core+wsi-20160715
arch=(any)
url="https://www.khronos.org/vulkan/"
license=('custom')
makedepends=(asciidoc)
groups=(vulkan-devel)
source=("https://github.com/KhronosGroup/${_pkgname}/archive/v${_pkgver}.tar.gz")
md5sums=('e9746b008106dc875f675e4351800452')

build() {
  cd "${_pkgname}-${_pkgver/+/-}/doc/specs/vulkan"

  make manpages
  make chunked
}

package_vulkan-headers() {
  pkgdesc="Vulkan header files"
  cd "${_pkgname}-${_pkgver/+/-}"

  install -dm755 ${pkgdir}/usr/include/vulkan
  install -dm755 ${pkgdir}/usr/share/vulkan

  install -m644 src/vulkan/vk_platform.h ${pkgdir}/usr/include/vulkan/
  install -m644 src/vulkan/vulkan.h ${pkgdir}/usr/include/vulkan
  install -m644 src/spec/vk.xml ${pkgdir}/usr/share/vulkan

  install -D -m644 doc/specs/vulkan/copyright.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright.txt
}

package_vulkan-man-pages() {
  pkgdesc="Vulkan man pages"
  cd "${_pkgname}-${_pkgver/+/-}"

  install -dm755 "${pkgdir}/usr/share/man/man3"
  install -m644 out/1.0/man/3/*.3 "${pkgdir}/usr/share/man/man3/"

  install -D -m644 doc/specs/vulkan/copyright.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright.txt
}

package_vulkan-html-docs() {
  pkgdesc="Vulkan html documentation"
  cd "${_pkgname}-${_pkgver/+/-}"

  install -dm755 "${pkgdir}/usr/share/doc/vulkan"
  mv -v out/1.0/vkspec.chunked/* "${pkgdir}/usr/share/doc/vulkan/"

  install -D -m644 doc/specs/vulkan/copyright.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright.txt
}
