# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - hold for toolchain update, only build for v7

# Guinnux: Ben Phillips <ben@kses.net>
#  - fixed for cross-compilation and fixed cross tuple check in configure

buildarch=4
noautobuild=1

pkgname=valgrind
pkgver=3.12.0
pkgrel=1
pkgdesc='A tool to help find memory-management problems in programs'
arch=(i686 x86_64)
license=(GPL)
url='http://valgrind.org/'
depends=('glibc>=2.21' 'glibc<2.22' 'perl')
makedepends=('gdb' 'openmpi')
optdepends=('openmpi: MPI support')
options=('!emptydirs')
source=(http://valgrind.org/downloads/$pkgname-$pkgver.tar.bz2
        valgrind_configure_arch_fix.patch)
sha1sums=('7a6878bf998c60d1e377a4f22ebece8d9305bda4'
          'ff9e20d05e8bf96b6af001d56b2440598d576c9e')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i "$srcdir"/valgrind_configure_arch_fix.patch
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --host=$CHOST --mandir=/usr/share/man --with-mpicc=mpicc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
