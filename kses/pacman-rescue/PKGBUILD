# vim: set ts=2 sw=2 et:
# $Id: PKGBUILD 146700 2012-01-16 19:48:37Z dreisner $
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Dave Reisner <dreisner@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - arch/host/flags for arm, armv6h, and armv7h
#  - pacman.conf: architecture=arm/armv6h/armv7h, added our aur and alarm repos
#  - makepkg.conf: adjusted C/CXX/LDFLAGS
#  - patch to sync filesystem after install/remove

pkgname=pacman-rescue
pkgver=4.2.1
pkgrel=2
pkgdesc="A library-based package manager with dependency support"
arch=('i686' 'x86_64' )
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base' 'base-devel')
depends=('libarchive>=3.1.2')
makedepends=('asciidoc')   # roundup patch alters docs
checkdepends=('python2' 'fakechroot')
optdepends=('fakeroot: for makepkg usage as normal user')
provides=('pacman-contrib')
conflicts=('pacman-contrib')
replaces=('pacman-contrib')
backup=(etc/pacman.conf etc/makepkg.conf)
options=('strip' 'debug')
source=(https://sources.archlinux.org/other/pacman/pacman-$pkgver.tar.gz
        ensure-matching-database-and-package-version.patch
        0001-Sychronize-filesystem.patch
        pacman.conf
        makepkg.conf)

md5sums=('2a596fc8f723e99660c0869a74afcf47'
         'e8f72afe6f417d11bd36ada042744fe4'
         '291123878ec33ca8a3020ac85f2e91d1'
         '1cc2d1b7af069b720676e4b00d09baf6'
         'c88c41076f26e97c6915d8b967df96e7')

prepare() {
  cd $srcdir/pacman-$pkgver

  patch -p1 -i $srcdir/ensure-matching-database-and-package-version.patch
  patch -p1 -i ../0001-Sychronize-filesystem.patch
}

build() {
  cd "pacman-$pkgver"
  ac_cv_func_malloc_0_nonnull=yes ./configure --prefix=/usr \
              --host=$CHOST \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --disable-doc  \
              --with-scriptlet-shell=/usr/bin/bash  \
              --without-openssl \
              --without-libcurl 
  make
  make -C contrib
}

package() {
  cd "pacman-$pkgver"

  make -j1 DESTDIR="$pkgdir" install
  make -j1 DESTDIR="$pkgdir" -C contrib install

  # install Arch specific stuff
  install -dm755 "$pkgdir/etc"
  install -m644 "$srcdir/pacman.conf" "$pkgdir/etc/pacman.conf"

  case $CARCH in
    arm)
      mycarch="arm"
      mychost="armv5tel-unknown-linux-gnueabi"
      myflags="-march=armv5te "
      ;;
    armv6h)
      mycarch="armv6h"
      mychost="armv6l-unknown-linux-gnueabihf"
      myflags="-march=armv6 -mfloat-abi=hard -mfpu=vfp "
      ;;
    armv7h)
      mycarch="armv7h"
      mychost="armv7l-unknown-linux-gnueabihf"
      myflags="-march=armv7-a -mfloat-abi=hard -mfpu=vfpv3-d16 "
      ;;
    aarch64)
      mycarch="aarch64"
      mychost="aarch64-unknown-linux-gnu"
      myflags="-march=armv8-a "
      ;;
  esac

  # set things correctly in the default conf file
  install -m644 "$srcdir/makepkg.conf" "$pkgdir/etc"
  sed -i "$pkgdir/etc/makepkg.conf" \
    -e "s|@CARCH[@]|$mycarch|g" \
    -e "s|@CHOST[@]|$CHOST|g" \
    -e "s|@CARCHFLAGS[@]|$myflags|g"
  sed -i $pkgdir/etc/pacman.conf -e "s|@CARCH[@]|$mycarch|g"

  # put bash_completion in the right location
  install -dm755 "$pkgdir/usr/share/bash-completion/completions"
  mv "$pkgdir/etc/bash_completion.d/pacman" "$pkgdir/usr/share/bash-completion/completions"
  rmdir "$pkgdir/etc/bash_completion.d"

  for f in makepkg pacman-key; do
    ln -s pacman "$pkgdir/usr/share/bash-completion/completions/$f"
  done

  install -Dm644 contrib/PKGBUILD.vim "$pkgdir/usr/share/vim/vimfiles/syntax/PKGBUILD.vim"

}
