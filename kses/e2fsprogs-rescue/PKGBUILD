# $Id: PKGBUILD 233393 2015-03-12 13:09:41Z ronald $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=e2fsprogs-rescue
pkgver=1.42.13
pkgrel=1
pkgdesc="Ext2/3/4 filesystem utilities - for the rescue filesystem"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'MIT')
url="http://e2fsprogs.sourceforge.net"
groups=('base')
depends=('sh')
provides=('e2fsprogs')
makedepends=('bc')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/e2fsprogs-${pkgver}.tar.gz"
        'CVE-2015-1572.patch' 'MIT-LICENSE')
backup=('etc/mke2fs.conf')
options=('staticlibs' 'strip')
install=e2fsprogs.install

sha1sums=('77d1412472ac5a67f8954166ec16c37616074c37'
          '7d0a46b59ea298e517f983f8ff67d29a0307f1ba'
          'f4a0d5b0cdb980e3fedd6f5e7dde0b0ffb7bbdfb')

prepare() {
  cd "${srcdir}/e2fsprogs-${pkgver}"

  # Remove unnecessary init.d directory
  sed -i '/init\.d/s|^|#|' misc/Makefile.in

  # backport CVE 2015-1572 fix
  # patch -Np1 -i "${srcdir}/CVE-2015-1572.patch"
}

build() {
  cd "${srcdir}/e2fsprogs-${pkgver}"

  ./configure --prefix=/usr \
              --host=$CHOST \
              --with-root-prefix="" \
              --libdir=/usr/lib \
              --sbindir=/usr/bin \
              --disable-nls \
              --disable-rpath \
              --enable-symlink-install \
              --enable-symlink-build

  make
}

package() {
  unset MAKEFLAGS

  cd "${srcdir}/e2fsprogs-${pkgver}"
  make DESTDIR="${pkgdir}" install install-libs

  sed -i -e 's/^AWK=.*/AWK=awk/' "${pkgdir}/usr/bin/compile_et"

  # remove references to build directory
  sed -i -e 's#^SS_DIR=.*#SS_DIR="/usr/share/ss"#' "${pkgdir}/usr/bin/mk_cmds"
  sed -i -e 's#^ET_DIR=.*#ET_DIR="/usr/share/et"#' "${pkgdir}/usr/bin/compile_et"

  # remove static libraries with a shared counterpart
  rm "${pkgdir}"/usr/lib/lib{com_err,e2p,ext2fs,ss}.a

  # install MIT license
  install -Dm644 "${srcdir}/MIT-LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/MIT-LICENSE"
}
